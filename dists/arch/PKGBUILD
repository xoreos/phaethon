# Maintainer: Sven Hesse <drmccoy@drmccoy.de>

pkgname=phaethon-git
_gitname=phaethon
pkgver=0.0.4.r0.g5020409
pkgrel=1
pkgdesc="A resource explorer for BioWare's Aurora engine games"
arch=('i686' 'x86_64')
url="https://xoreos.org/"
license=('GPL3')
depends=('zlib' 'xz' 'boost' 'boost-libs' 'openal' 'libmad' 'libogg' 'libvorbis' 'faad2' 'xvidcore' 'qt5-multimedia')
makedepends=()
source=('git://github.com/xoreos/phaethon.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"

	git describe --long --match desc/\* | cut -d '/' -f 2- | sed -e 's/\(.*\)-\([^-]*\)-\([^-]*\)/\1.r\2.\3/'
}

build() {
	cd "$srcdir/$_gitname"

	./autogen.sh
	./configure --with-release=xArch --with-lto --prefix=/usr
	make
}

package() {
	cd "$srcdir/$_gitname"

	make DESTDIR="$pkgdir/" install
}
